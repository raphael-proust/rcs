#Custom version based on Arch's package

pkgname=dvtm
pkgver=0.10
pkgrel=100
pkgdesc='Dynamic virtual terminal manager.'
arch=('i686' 'x86_64' 'arm')
url='http://www.brain-dump.org/projects/dvtm/'
license=('MIT')
depends=('ncurses')
source=("http://www.brain-dump.org/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'config.h')
sha1sums=(
'00e3d6cb746f8eace07e6784452d53781e76db13'
'adbc93a345f1bfa59b857f36b42f71c79561d2a5'
)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	cp "${srcdir}/config.h" .
	sed -i 's/CFLAGS =/CFLAGS +=/' config.mk

	make clean
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
