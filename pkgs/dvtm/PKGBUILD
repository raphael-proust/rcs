# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: bender02 at gmx dot com

pkgname=dvtm
pkgver=0.15
pkgrel=4
pkgdesc='Dynamic virtual terminal manager.'
arch=('x86_64')
url='http://www.brain-dump.org/projects/dvtm/'
license=('MIT')
depends=('ncurses' 'laptop-status')
source=(
"http://www.brain-dump.org/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz"
'config.h'
'dvtm-status'
)
md5sums=(
887e162a3abe2ad8e86caefab20cdd63
bebc4fd10765b0cc9c9e705943b5feeb
4baa7c5554d750932e5dad8bffdd3184
)

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	cp "${srcdir}/config.h" .
	cp "${srcdir}/dvtm-status" .
	sed -i 's/CFLAGS =/CFLAGS +=/' config.mk

	if ls "${startdir}"/patches | grep -q '.*\.diff$'
	then
		for i in "${startdir}"/patches/*\.diff
		do
			msg "patching with $i"
			patch -i $i
		done
	fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make clean
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	# this is provided by ncurses now
	rm -rf "${pkgdir}/usr/share/terminfo/"
}
