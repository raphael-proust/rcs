# Handcrafted from the Archlinux original package

pkgbase=vim
pkgname=('vim' 'vim-runtime')
_topver=7.3
_versiondir="vim${_topver//./}"
pkgver=4253
pkgrel=2
arch=('i686' 'x86_64' 'arm')
license=('custom:vim')
url="http://www.vim.org"
depends=('python2')
makedepends=('mercurial')

_hgroot='http://vim.googlecode.com/hg/'
_hgrepo='vim'

build() {
  # remove -O2 because there is a crash with python because of it
  # fedora br: https://bugzilla.redhat.com/show_bug.cgi?id=817196
  export CFLAGS="${CFLAGS/-O2 /}"

  cd "${srcdir}"
  cd "${_hgrepo}" && hg pull -u && cd .. ||
  hg clone "${_hgroot}${_hgrepo}"

  cp -a ${_hgrepo} vim-build
  rm -rf "${srcdir}/runtime-install"

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
  # fix python name
  sed -i -e 's|(vi_cv_path_python, python)|(vi_cv_path_python, python2)|' \
    vim-build/src/configure.in
  (cd vim-build/src && autoconf)

  cd "${srcdir}"/vim-build

  ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --with-features=big --with-compiledby=bnwr \
    --disable-gpm --enable-acl --with-x=no \
    --disable-mouse \
    --disable-gui --enable-multibyte --enable-cscope \
    --disable-netbeans --enable-perlinterp --enable-pythoninterp \
    --disable-python3interp --disable-rubyinterp --disable-luainterp

  make -j 4

}

package_vim() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
  depends=("vim-runtime=${pkgver}-${pkgrel}")
  conflicts=('gvim')

  cd "${srcdir}"/vim-build
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}"/usr/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Move the runtime for later packaging
  mv "${pkgdir}"/usr/share/vim "${srcdir}"/runtime-install

  # license
  install -Dm644 "${srcdir}"/vim-build/runtime/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}

package_vim-runtime() {
  pkgdesc='Runtime for vim'
  depends=('python2' 'perl' 'gawk')

  # Install the runtime split from vim
  install -dm755 "${pkgdir}"/usr/share
  mv "${srcdir}"/runtime-install "${pkgdir}"/usr/share/vim

  # Don't forget logtalk.dict
  install -Dm644 "${srcdir}"/vim-build/runtime/ftplugin/logtalk.dict \
    "${pkgdir}"/usr/share/vim/${_versiondir}/ftplugin/logtalk.dict

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    "${pkgdir}"/usr/share/vim/${_versiondir}/filetype.vim

  # patch filetype.vim for better handling of pacman related files
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    "${pkgdir}"/usr/share/vim/${_versiondir}/filetype.vim
  sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
    "${pkgdir}"/usr/share/vim/${_versiondir}/ftplugin/changelog.vim

  # rgb.txt file
  install -Dm644 "${srcdir}"/vim-build/runtime/rgb.txt \
    "${pkgdir}"/usr/share/vim/${_versiondir}/rgb.txt

  # license
  install -dm755 "${pkgdir}"/usr/share/licenses/vim-runtime
  ln -s /usr/share/vim/${_versiondir}/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/vim-runtime/license.txt
}
