# Handcrafted from Archlinux's original package

pkgname=dwm
pkgver=6.0
pkgrel=120
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
  config.h
  config.mk)
md5sums=(
8bb00d4142259beb11e13473b81c0857
573b0b5bae1fda171681b3f866b9e91c
eab9c0052a884f8b2d08cddf83f1fe62
)

build() {
  cd $srcdir/$pkgname-$pkgver
  cp $srcdir/config.mk config.mk
  cp $srcdir/config.h config.h

  if ls $startdir/patches | grep -q '.*\.diff$'
  then
    for i in $startdir/patches/*\.diff
    do
      msg "patching with $i"
      patch -i $i
    done
  fi

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
}
