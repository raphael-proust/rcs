#Custom build based on Arch's official pakage

pkgname=surf
pkgver=0.6
pkgrel=100
pkgdesc="A simple web browser based on WebKit/GTK+."
arch=('i686' 'x86_64')
url='http://surf.suckless.org/'
license=('MIT')
depends=('libwebkit' 'xorg-xprop')
optdepends=('dmenu: URL-bar'
            'ca-certificates: SSL verification'
            'st: default download handler'
            'curl: default download handler')
makedepends=('pkgconfig')
source=("http://dl.suckless.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'config.h')
md5sums=('aeeed723b562a30cc6a2b3ea18f6d99a'
         'd0fb809836e765fdf43c194f7a4ae3cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/config.h" config.h

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
